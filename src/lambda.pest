WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }

ident = @{ (alpha | digit)+ }

start = { SOI ~ expr ~ EOI }

expr = { pi | exprLambda }
exprLambda = { lambda | exprArrow }
exprArrow = { arrow | exprAnnotated }
exprAnnotated = { annotated | exprApp }
exprApp = { app | exprAtomic }
exprAtomic = { var | star | parenExpr }

parenExpr = {"(" ~ expr ~ ")"}

lambda = {("lambda"|"\\"|"λ") ~ ident ~ "." ~ expr}

pi = {("forall"|"∀") ~ ident ~ ":" ~ expr ~ "." ~ expr}

app = {exprAtomic ~ expr}

var = {ident}

annotated = {exprApp ~ ":" ~ expr}

arrow = {exprAnnotated ~ "->" ~ exprArrow}

star = { "*" }

